PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
# Extrait la liste des Named Graphs

PREFIX dms: <https://schema.ld.admin.ch/datamngt#>
PREFIX sfa: <https://lindas.admin.ch/sfa/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <http://schema.org/>
PREFIX blueprintConfig: <https://described.at/zazuko/blueprintConfig/>
PREFIX flux: <https://flux.described.at/>
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX ld-schema: <https://schema.ld.admin.ch/>

CONSTRUCT {
  GRAPH sfa:dataMngt {
    dms:NamedGraph  a rdfs:Class;
                    rdfs:comment "Class of entities those URI are used to identify Named Graphs in the triplestore.";
                    rdfs:label "Class of Named Graphs". 
    dms:hasOwnerAbbr  a rdf:Property;
   	                  rdfs:domain dms:NamedGraph;
                      rdfs:range xsd:string;
                      rdfs:comment "has as owner this organisation's abbreviation".
    dms:hasOwnerOrganization  a rdf:Property;
      						            rdfs:domain dms:NamedGraph;
    	  					            rdfs:range schema:Governmentorganization;
    		  				            rdfs:comment "has a defined governmental organization as owner of this graph".
 
    ?g  a dms:NamedGraph ;
        rdfs:label ?label;
    	  schema:name ?name.
    # allows to assign multiple organisation entries
    ?g dms:hasOwnerOrganization ?organisation.
    # copying part of organisation information (for Fuseki only)
  	?organisation a schema:GovernmentOrganization;
    				      rdfs:label ?labelDE, ?labelEN.
  }
} 
WHERE {
  GRAPH ?g { }
  # process named graph of Stadt ZÃ¼rich the same way as LINDAS named graphs
  BIND (REPLACE(STR(?g), "^https://ld.stadt-zuerich.ch/statistics", "https://lindas.admin.ch/stadtzuerich") AS ?str1)
  # Few named graphs have a "/" at the end
  BIND (REPLACE(?str1, "/$" , "") AS ?str2)
  # Remove the domain part
  BIND (REPLACE(?str2, "^https://[a-z,0-9-.]+/", "") AS ?str3)
  # Some named graphs are not assigned to a specific organisation. Name this organisation LINDAS
  BIND (if(contains(?str3,"/"),?str3,concat("LINDAS/",?str3)) As ?str4)
  # Transform the organisation part in capitals
  BIND (UCASE(REPLACE(?str4, "/.+$", "")) AS ?abbrCap)
  BIND (STRLANG(?abbrCap, "en") AS ?abbrENCap)
  # Consider the last part as the name of the named graph
  BIND (REPLACE(?str4, "^.+/", "") AS ?name)
  BIND (CONCAT(?abbrCap, "-", ?name) AS ?label)
    
  # Lookup for the German abbreviation
  OPTIONAL {
    ?abbrEntry  a ld-schema:Abbreviation;
    	          schema:name ?abbrEN, ?abbrDE1.
    FILTER (UCASE(STR(?abbrEN)) = ?abbrCap)
    FILTER (lang (?abbrDE1) = "de" && lang(?abbrEN) = "en")
  }
  BIND (IF (BOUND (?abbrEntry), ?abbrDE1, STRLANG(?abbrCap, "de")) AS ?abbrDE)
    
  # Lookup for the organisation itself in the different graphs.
  # There is room for standardisation and consolidation
  # Case 1: the German abbreviation is found in Staatskalender
  OPTIONAL {
    GRAPH <https://lindas.admin.ch/fch/staatskalender> {
      ?org1 a schema:GovernmentOrganization;
            schema:alternateName ?abbrDE.
    }
  }
  # Case 2: There was no abbreviation found in Termdat (ex. Agroscope) amd we must search with case-incensitive
  OPTIONAL {
    GRAPH <https://lindas.admin.ch/fch/staatskalender> {
      ?org2 a schema:GovernmentOrganization;
            schema:alternateName ?abbrDE2.
    }
    FILTER (UCASE(STR(?abbrDE)) = UCASE(STR(?abbrDE2)))
  }
  # Case 3: We search for this abbreviation into other graphs
  OPTIONAL {
    ?org3 a schema:GovernmentOrganization; schema:alternateName ?abbrDE.
  }
  # Case 4: Lookup for a company in Zefix with a search restricted to the end of the company name
  OPTIONAL {
    ?org4 a ld-schema:ZefixOrganisation;
          schema:legalName ?companyName;
          schema:additionalType <https://ld.admin.ch/ech/97/legalforms/0117>.
    FILTER STRENDS(STR(?companyName), STR(?abbrDE))
  }
  
	BIND ( 
    IF ( BOUND(?org1),?org1, 
    IF ( BOUND(?org2),?org2, 
    IF ( BOUND(?org3),?org3,
    IF ( BOUND(?org4),?org4, <https://ld.admin.ch/unknownOrganization>)))) AS ?organisation).
  BIND (
    IF ( BOUND(?org1) || BOUND(?org2) || BOUND(?org3) || BOUND(?org4), ?abbrDE, STRLANG("Unbekannt", "de")) AS ?labelDE).
  BIND (
    IF ( BOUND(?org1) || BOUND(?org2) || BOUND(?org3) || BOUND(?org4), ?abbrEN, STRLANG("Unknown", "en")) AS ?labelEN).
}
