PREFIX dms: <https://schema.ld.admin.ch/datamngt#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX sfa: <https://lindas.admin.ch/sfa/>
PREFIX blueprintConfig: <https://described.at/zazuko/blueprintConfig/>
PREFIX flux: <https://flux.described.at/>
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX schema: <http://schema.org/>
PREFIX cube: <https://cube.link/>
PREFIX cube-meta: <https://cube.link/meta/>

CONSTRUCT {
    GRAPH sfa:dataMngt {  
        dms:containsDataset a rdf:Property;
                            rdfs:domain dms:NamedGraph;
                            rdfs:range dcat:Dataset.
        
        ?g dms:containsDataset ?termset.
        ?termset a dms:Vocabulary.
        ?termset rdfs:label ?termsetName.
        ?termset dms:isUsedIn ?cube.
    }
}
#SELECT ?termset ?cardinality ?termsetName1 ?cube
WHERE {
    GRAPH ?g {
        ?termset a cube-meta:SharedDimension.
    }
    OPTIONAL {?termset cube-meta:isUsedIn ?cube}

    {
        SELECT ?termset ?termsetName1 (count(?cube) AS ?cardinality)
        WHERE {
#    BIND (<https://ld.admin.ch/time/department> AS ?termset)
            ?termset a cube-meta:SharedDimension.
            ?termset schema:name ?termsetName0.
            OPTIONAL {?termset cube-meta:isUsedIn ?cube.}
            FILTER langMatches (lang(?termsetName0), "EN")
            BIND(STR(?termsetName0) AS ?termsetName1)
        } GROUP BY ?termset ?termsetName1
    }
    BIND (concat("SD (",STR(?cardinality),")-",?termsetName1) AS ?termsetName)
}
