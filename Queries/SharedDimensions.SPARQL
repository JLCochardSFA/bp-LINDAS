# Identify all the vocabularies marked as Shared Dimension
# Build a name with in parentheses the number of cubes using this vocabulary
# And relate to each of them

PREFIX dms: <https://schema.ld.admin.ch/datamngt#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX sfa: <https://lindas.admin.ch/sfa/>
PREFIX blueprintConfig: <https://described.at/zazuko/blueprintConfig/>
PREFIX flux: <https://flux.described.at/>
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX schema: <http://schema.org/>
PREFIX cube: <https://cube.link/>
PREFIX cube-meta: <https://cube.link/meta/>

CONSTRUCT {
    GRAPH sfa:dataMngt {  
        dms:containsDataset a rdf:Property;
                            rdfs:domain dms:NamedGraph;
                            rdfs:range dcat:Dataset.
        
        ?g dms:containsDataset ?termset.
        ?termset a dms:SharedDimension.
        ?termset rdfs:label ?termsetName.
        ?termset dms:isUsedIn ?cube.
        ?termset dms:Cardinality ?cardinality.
    }
}

WHERE {
    GRAPH ?g {
        ?termset a cube-meta:SharedDimension.
    }
    OPTIONAL {?termset cube-meta:isUsedIn ?cube}

    {
        SELECT ?termset ?termsetName1 (count(?cube) AS ?cardinality)
        WHERE {
            ?termset a cube-meta:SharedDimension.
            ?termset schema:name ?termsetName0.
            OPTIONAL {?termset cube-meta:isUsedIn ?cube.}
            FILTER langMatches (lang(?termsetName0), "EN")
            BIND(STR(?termsetName0) AS ?termsetName1)
        } GROUP BY ?termset ?termsetName1
    }
    BIND (concat(?termsetName1," (",STR(?cardinality),")") AS ?termsetName)
}
