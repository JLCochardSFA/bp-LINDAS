# Extrait la liste des datasets qui sont associés à des cubes sur la base de la classe dcat/Dataset
# Met en lien le dataset avec le Named Graph qui le contient et le dataset avec les cubes qui le 
# constituent.

PREFIX dms: <https://schema.ld.admin.ch/datamngt#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX sfa: <https://lindas.admin.ch/sfa/>
PREFIX blueprintConfig: <https://described.at/zazuko/blueprintConfig/>
PREFIX flux: <https://flux.described.at/>
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX schema: <http://schema.org/>
PREFIX cube: <https://cube.link/>
PREFIX cube-meta: <https://cube.link/meta/>

CONSTRUCT {
    GRAPH sfa:dataMngt {
        dms:containsDataset a rdf:Property;
                            rdfs:domain dms:NamedGraph;
                            rdfs:range dcat:Dataset.
 		
    ?g dms:containsDataset ?dataset.
    ?dataset a dms:Dataset.
    ?dataset rdfs:label ?datasetName.
    ?dataset dms:hasVersion ?cube.
    ?cube a dms:Cube.
    ?cube rdfs:label ?cubeVersionName.
    }
}
WHERE {
    GRAPH ?g {
        ?cube a cube:Cube.
        OPTIONAL {?cube schema:name ?cubeName0.}
        OPTIONAL {
            ?cube schema:version ?cubeVersion.
             BIND (STRLANG(STR(?cubeVersion), "en") AS ?cubeVersionName0)
        }
        BIND (COALESCE(?cubeVersionName0,?cubeName0,STRLANG("Not named","en")) AS ?cubeVersionName)
        ?dataset schema:hasPart ?cube.
        OPTIONAL {?dataset schema:name ?datasetName0.}
        BIND (COALESCE(?datasetName0,?cubeName0,STRLANG("Not named","en")) AS ?datasetName)
        FILTER langMatches( lang(?datasetName), "EN")
        FILTER langMatches( lang(?cubeVersionName), "EN")
    }
}                  
